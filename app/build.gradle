apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId 'ch.sheremet.dasfoo.rover.android.client'
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.LintOptions.html

        // https://github.com/square/okio/issues/58
        // A part of okio.Okio references a missing (on Android) package java.nio.file.
        // We don't use that part, but it generates an error. Downgrade it to warning.
        // TODO(dotdoom): use lint.xml instead to disable this warning specifically for okio.
        warning 'InvalidPackage'

        // Enable all warnings, even those disabled by default.
        checkAllWarnings true

        // Generate a text report that might be seen in the CI build log.
        textReport true
        textOutput "stdout"
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:0.13.2' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                javanano {
                    // Options added to --javanano_out
                    option 'ignore_services=true'
                }
            }

            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'nano=true'
                    option 'nano'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'io.grpc:grpc-okhttp:0.14.1'
    compile 'io.grpc:grpc-protobuf-nano:0.14.1'
    compile 'io.grpc:grpc-stub:0.14.1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.google.android.gms:play-services-safetynet:9.0.2'
}
