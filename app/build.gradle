apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 23

    // https://developer.android.com/studio/releases/build-tools.html
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'org.dasfoo.rover.android.client'
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            // It detects any code or libraries that aren't being used
            // and ignores them from your final apk.
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            // Adds .debug to the application package name
            // to be able to install both debug and release apk on the same device
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.LintOptions.html

        // Enable all warnings, even those disabled by default.
        checkAllWarnings true

        // Generate a text report that might be seen in the CI build log.
        textReport true
        textOutput "stdout"
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:0.15.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                javanano {
                    // Options added to --javanano_out
                    option 'ignore_services=true'
                }
            }

            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'nano=true'
                    option 'nano'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    // According to lint, major version number should match compileSdkVersion.
    compile 'com.android.support:appcompat-v7:23.4.0'

    compile 'io.grpc:grpc-okhttp:0.15.0'
    compile 'io.grpc:grpc-protobuf-nano:0.15.0'
    compile 'io.grpc:grpc-stub:0.15.0'

    compile 'javax.annotation:javax.annotation-api:1.2'

    compile 'com.google.android.gms:play-services-safetynet:9.2.0'
}
